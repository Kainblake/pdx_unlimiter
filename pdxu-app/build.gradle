import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id "org.moditect.gradleplugin" version "1.0.0-rc3"
}

java {
    modularity.inferModulePath = true
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

def currentOS = DefaultNativePlatform.currentOperatingSystem;
def suffix = ''
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
    def macArch = System.getProperty ("os.arch");
    if (macArch == 'aarch64') {
        suffix = '-aarch64'
    }
}

apply from: "$rootDir/gradle/module_gen.gradle"
apply from: "$rootDir/gradle/jackson.gradle"

dependencies {
    implementation "org.openjfx:javafx-base:18:${platform}${suffix}"
    implementation "org.openjfx:javafx-controls:18:${platform}${suffix}"
    implementation "org.openjfx:javafx-graphics:18:${platform}${suffix}"
    implementation "org.openjfx:javafx-media:18:${platform}${suffix}"
    implementation 'com.jfoenix:jfoenix:9.0.10'

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-javafx', version: "11.3.5"
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-materialdesign-pack', version: "11.3.5"
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0-alpha1'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.0-alpha1'

    implementation files("$project.buildDir/generated-modules/com.realityinteractive.imageio.tga-master-SNAPSHOT.jar")
    implementation files("$project.buildDir/generated-modules/dds-1.0.0.jar")
    implementation files("$project.buildDir/generated-modules/commons-lang3-3.12.0.jar")
    implementation files("$project.buildDir/generated-modules/commons-io-2.8.0.jar")
    implementation files("$project.buildDir/generated-modules/commons-collections4-4.4.jar")
    implementation files("$project.buildDir/generated-modules/sentry-5.6.1.jar")
    implementation files("$project.buildDir/generated-modules/jnativehook-2.0.2.jar")

    implementation project(':pdxu-io')
    implementation project(':pdxu-model')
}
